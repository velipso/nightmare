
include 'nightmare' # required
using music

def invert chord
	var ret = chord[:]
	return list.unshift ret, (list.pop ret) - 7
end

reset 0, 12
tempo 0, 400000
var t = 0
var majorkey = {0, 2, 4, 5, 7, 9, 11}
var keyof = (note 'C3') + rand.int % 12
for var m: range 24
	var i = &majorkey - 7
	list.push majorkey, majorkey[i] + 12
end

var chordprog = {}
var lentot = 0
do while lentot < 96
	var base = num.floor rand.num * 7
	var chord = {
		base,
		base + 2,
		base + 4,
		base + 6
	}

	if rand.num < 0.667
		chord = invert chord
		if rand.num < 0.5
			chord = invert chord
		end
	end

	var len = 12 * ((rand.int % 2) + 1)
	if lentot + len > 96
		len = 96 - lentot
	end
	list.push chordprog, {base, chord, len}
	lentot += len
end

for var m: range 4
	for var ci: chordprog
		var base = ci[0]
		var chord = ci[1];
		var len = ci[2]

		var bn = keyof + majorkey[base] - 24
		noteplay t, len, bn
		noteplay t, len, bn - 12

		for var ch: chord[1:]
			var n = keyof + majorkey[ch + 7]
			noteplay t + 6, 6, n
		end

		t += len
	end
end
